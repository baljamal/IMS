VA Ve     ' MCode version

VA PU     ' Power up flag
VA p1     ' Saved position
VA Sd     ' Save deadband
VA Sv     ' Save flag
VA Ns     ' Number of saves
VA Us     ' Delay for status update
VA Sk     ' Scan or not
VA X1
VA X2

Ve 1      ' Version 1
Ne 0      ' Disable numeric cmd
p1 P      ' Start with current position
Sd Db*4   ' Set save deadband
Sv 0
Sk 0
Us 18000

PG 1
  LB SU	            ' Run at power up

  H 1000            ' 1 second hold let motor boot-up

  PU 1	            ' Set the Power-Up flag
  P p1         	    ' Restore last saved position

  R1 0              ' Reset the moving flag
  R2 0	            ' Reset the delay counter
  R3 0	            ' Reset the moved/slipped flag
  R4 0	            ' Reset the delay counter

  Us 18000
  Sk 0

  LB Lp
    CL Ya, Mv=1	    ' Moving
    CL Za, Mv=0	    ' Not moving
    H  1
    BR Lp

  LB Ya
    CL Yb, R1=1     ' Has been moving
    CL Yc, R1=0     ' Started moving
    RT

  LB Yb
    IC R2           ' Increment the delay counter
    CL Rp, R2>30    ' Has been 0.3 second, send report
    RT

  LB Yc
    R1 1            ' Set the moving flag
    R2 0            ' Reset the delay counter
    Us 30
    RT

  LB Za
    CL Zc, R1=0     ' Was not moving
    CL Zb, R1=1     ' Just finished moving
    RT

  LB Zb
    CL Rp

    R1 0            ' Clear the moving flag
    R2 Us-50        ' Clear the moving flag
    R3 1            ' Delay started by a move
    R4 1            ' Start the delay counter for saving
    RT

  LB Zc
    CL Zd, Sv=9     ' Save now

    CL Ze, Sk=0     ' Not scanning

    IC R2
    CL Rp, R2>Us    ' Update status
    RT

  LB Zd
    p1 P            ' Assign P to P1
    S	            ' Save to NVM
    IC Ns           ' Increment the Save counter
    R3 0            ' Reset the moved/slipped flag
    R4 0            ' Reset the delay counter
    Sv 0            ' Reset the save flag
    PR "Saved ",P1
    RT

  LB Ze
    CL Zf, R4>0     ' Moved or slipped, delaying for saving
    CL Zg, R4=0     ' Check for slipping
    CL Zi, R4>3000  ' Has delayed about 45 seconds
    RT

  LB Zf
    IC R4
    RT

  LB Zg
    X1 p1-Sd
    X2 p1+Sd
    CL Zh, P<X1	    ' Slipped below deadband
    CL Zh, P>X2	    ' Slipped above deadband
    RT

  LB Zh
    CL Rp

    R3 0            ' Delay started by slipping
    R4 1            ' Start the delay counter
    RT

  LB Zi
    CL Zj, R3=0     ' Delay started by slipping
    CL Zk, R3=1	    ' Delay started by a move
    RT

  LB Zj
    X1 p1-Sd
    X2 p1+Sd
    CL Zk, P<X1	    ' Really slipped below deadband
    CL Zk, P>X2	    ' Really slipped above deadband
    CL Zm, P>=X1    ' Came back or not
    RT

  LB Zk
    CL Zl, Sv=0
    RT

  LB Zl
    Sv 1
    PR "Want2Save"
    RT

  LB Zm
    CL Zn, P<=X2    ' Came back
    RT

  LB Zn
    R4 0            ' Came back, reset the delay counter
    RT

  LB Rp
    CL Ra, Sk=0
    CL Rb, Sk=1

    R2 0            ' Reset the delay counter
    CL Rd, Us=0     ' Reset the status update delay
    RT

  LB Ra
    X2 NE*32768     ' bit-16 is the numeric enable

    X1 PU*16384     ' bit-15 is the power-up flag
    X2 X2+X1

    X1 ST* 8192     ' bit-14 is stall detection
    X2 X2+X1

    X1 ER*   64     ' bit-7 through 13 is the error code
    X2 X2+X1

    X1 I4*   32     ' bit-6 is I4
    X2 X2+X1

    X1 I2*   16     ' bit-5 is I2
    X2 X2+X1

    X1 I1*    8     ' bit-4 is I1
    X2 X2+X1

    X1 SM*    4     ' bit-3 is stall detection mode
    X2 X2+X1

    X1 EE*    2     ' bit-2 is encoder enable
    X2 X2+X1

    X2 X2+MV        ' LSB is MV

    PR "BOS", X2, ",P=", P, "EOS"
    RT

  LB Rb
    X2 ST* 8192     ' bit-14 is stall detection
    X2 X2+MV        ' LSB is MV

    PR "BOS", X2, ",P=", P, "EOS"
    RT

  LB Rd
    Us 18000
    RT

E
PG

