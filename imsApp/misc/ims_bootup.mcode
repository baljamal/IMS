VA Ve     ' MCode version

VA PU     ' Power up flag
VA p1     ' Saved position
VA Sd     ' Save deadband
VA Sv     ' Save flag
VA Ns     ' Number of saves
VA Us     ' Delay for status update
VA Sk     ' Scan or not
VA Sr     ' Scanning and moving
VA Os     ' Old state
VA Xl
VA Xh
VA X1
VA X2

Ve 1      ' Version 1
Ne 0      ' Disable numeric cmd
p1 P      ' Start with current position
Sd Db*4   ' Set save deadband
Sv 0
Sk 0
Us 18000

PG 1
  LB SU	            ' Run at power up

  H 1000            ' 1 second hold let motor boot-up

  PU 1              ' Set the Power-Up flag
  P p1         	    ' Restore last saved position

  R1 0              ' Reset the moving / slipping flag
  R2 0              ' Reset the delay counter for status update
  R3 0              ' Reset the delay counter for saving

  CL Bs
  Os X2

  Us 18000

  LB Lp
    CL Ya, Mv=1	    ' Moving
    CL Za, Mv=0	    ' Not moving
    H  1
    BR Lp

  LB Ya
    CL Yb, R1=1     ' Has been moving
    CL Yc, R1<>1    ' Started moving
    RT

  LB Yb
    IC R2           ' Increment the delay counter
    CL Rp, R2>30    ' Has been 0.3 second, update status
    RT

  LB Yc
    R1 1            ' Set the moving flag
    R2 1            ' Reset the delay counter
    Us 30
    RT

  LB Za
    CL Zc, R1<>1    ' Was not moving
    CL Zb, R1=1     ' Just finished moving
    RT

  LB Zb
    CL Rp

    R1 2            ' Delay started by a move
    RT

  LB Zc
    CL Zd, Sv=9     ' Save now

    IC R2

    CL Ze, R1>1     ' Delaying
    CL Zt, R1=0     ' Check for status change or slipping

    CL Zx, R3>0     ' Delay for saving
    RT

  LB Zd
    p1 P            ' Assign P to P1
    R3 0            ' Reset the delay counter
    Sv 0            ' Reset the save flag
    IC Ns           ' Increment the Save counter
    S               ' Save to NVM
    PR "Saved ",P1
    RT

  LB Ze
    CL Zf, R1>2
    CL Zr, R1=2
    RT

  LB Zf
    CL Zg, R2>5
    RT

  LB Zg
    CL Zl, R1=3
    CL Zn, R1=4
    CL Zm, R1=5

    R2 1
    RT

  LB Zl
    R1 0
    R3 1
    RT

  LB Zm
    R1 0
    RT

  LB Zn
    Xl p1-Sd
    Xh p1+Sd
    CL Zo, P<Xl     ' Continue to slip
    CL Zo, P>Xh     ' Continue to slip
    CL Zp, P>=Xl    ' Stopped slipping or not
    RT

  LB Zo
    PR "BOS", X2, ",P=", P, "EOS"
    p1 P

    CL Re, Us=0     ' Reset the status update delay
    RT

  LB Zp
    CL Zq, P<=Xh    ' Stopped slipping
    RT

  LB Zq
    R1 0
    R3 1
    RT

  LB Zr
    CL Zs, R2>30
    RT

  LB Zs
    CL Rp

    Os X2
    p1 P

    R1 3
    RT

  LB Zt
    CL Bs           ' Build the status word

    CL Zu, X2=Os    ' Check for slipping
    CL Zw, X2<>Os   ' Report immediately

    CL Rp, R2>Us
    RT

  LB Zu
    Xl p1-Sd
    Xh p1+Sd
    CL Zv, P<Xl	    ' Slipped below deadband
    CL Zv, P>Xh	    ' Slipped above deadband
    RT

  LB Zv
    CL Zo

    R1 4            ' Delay started by slipping
    R2 1            ' Reset the delay counter
    R3 0            ' Reset the delay counter
    RT

  LB Zw
    PR "BOS", X2, ",P=", P, "EOS"
    Os X2

    R1 5            ' Delay started by status change
    R2 1            ' Reset the delay counter
    CL Re, Us=0     ' Reset the status update delay
    RT

  LB Zx
    IC R3
    CL Zy, R3>3000  ' Has delayed about 45 seconds
    RT

  LB Zy
    CL Zz, Sv=0
    RT

  LB Zz
    Sv 1
    PR "Want2Save"
    RT

  LB Bs
    X2 NE*32768     ' bit-16 is the numeric enable

    X1 PU*16384     ' bit-15 is the power-up flag
    X2 X2+X1

    X1 ST* 8192     ' bit-14 is stall detection
    X2 X2+X1

    X1 ER*   64     ' bit-7 through 13 is the error code
    X2 X2+X1

    X1 I4*   32     ' bit-6 is I4
    X2 X2+X1

    X1 I2*   16     ' bit-5 is I2
    X2 X2+X1

    X1 I1*    8     ' bit-4 is I1
    X2 X2+X1

    X1 SM*    4     ' bit-3 is stall detection mode
    X2 X2+X1

    X1 EE*    2     ' bit-2 is encoder enable
    X2 X2+X1

    X2 X2+MV        ' LSB is MV
    RT

  LB Rp
    CL Ra, Sk=1
    CL Rb, Sk=0

    R2 0            ' Reset the delay counter
    CL Re, Us=0     ' Reset the status update delay
    RT

  LB Ra
    CL Rb, R1<>1
    CL Rd, R1=1
    RT

  LB Rb
    Sr 0

    CL Bs

    PR "BOS", X2, ",P=", P, "EOS"
    RT

  LB Rd
    Sr 1

    X2 ST* 8192     ' bit-14 is stall detection
    X2 X2+MV        ' LSB is MV

    PR "BOS", X2, ",P=", P, "EOS"
    RT

  LB Re
    Us 18000
    RT

E
PG

